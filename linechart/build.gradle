plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

ext.versionMajor = 1 // API Changes, adding big new feature, redesign the App
ext.versionMinor = 0 // New features in a backwards-compatible manner
ext.versionPatch = 1 // Backwards-compatible bug fixes
ext.versionClassifier = null // Pre-releases (alpha, beta, rc, SNAPSHOT...)

android {
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions {
        jvmTarget = kotlinJvmTarget
    }

    compileSdkVersion androidConfig.compileSdkVersion

    defaultConfig {
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        versionCode generateVersionCode(versionMajor, versionMinor, versionPatch)
        versionName generateVersionName(versionMajor, versionMinor, versionPatch, versionClassifier)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    resourcePrefix 'linechart_'
}

dependencies {

    //----------------------------------------------------------------------------------------------
    // Lib Dependencies
    //----------------------------------------------------------------------------------------------

    implementation appDependencies.androidxCore
    implementation appDependencies.androidxAnnotation

    //----------------------------------------------------------------------------------------------
    // Testing Dependencies
    //----------------------------------------------------------------------------------------------

    testImplementation testDependencies.junit
    testImplementation testDependencies.mockito
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.davidmiguel.linechart"
                artifactId = "line-chart"
                version = generateVersionName(versionMajor, versionMinor, versionPatch, versionClassifier)
                artifact androidSourcesJar
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}
